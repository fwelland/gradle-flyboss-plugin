apply plugin: 'groovy'
apply plugin: 'com.jfrog.bintray'

version = '0.1'

def uzer = hasProperty('btIdentity') ? btIdentity : System.getenv('btIdentity')
def creds = hasProperty('btIDCreds') ? btIDCreds : System.getenv('btIDCreds')

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.1'
    }
}

repositories {
    jcenter()
}


sourceSets {
    integrationTest {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    testCompile ("org.spockframework:spock-core:0.7-groovy-2.0"){
        exclude group: 'org.codehaus.groovy'
    }
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}

bintray {
    user = uzer
    key = creds
    
    filesSpec { 
        from 'build/libs'
        into "org/fhw/gradle-flyboss-plugin/$version"
        include '*.jar'
    }
    
    dryRun = false //Whether to run this as dry-run, without deploying
    publish = true //If version should be auto published after an upload
    pkg {
        repo = 'FredsStuff'
        name = 'gradle-flyboss-plugin'
        desc = 'simple gradle plugin for basic wildfly/jboss server actions on local server instance'
        websiteUrl = 'http://stupidfredtricks.blogspot.com/2014/08/a-simple-gradle-plugin-for-wildfly-and.html'
        vcsUrl = 'https://github.com/fwelland/gradle-flyboss-plugin.git'
        licenses = ['Apache-2.0']
        labels = ['gradle', 'wildfly', 'java', 'jboss']        
                 
    }
}

task wrapper(type:Wrapper){
    gradleVersion = '2.0'
}

